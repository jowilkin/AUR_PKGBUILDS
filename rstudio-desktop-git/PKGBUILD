# Maintainer: JT Wilkinson <jowilkin@tu dot archserver dot org>
# Note: Apache ant and java must be on the path for make to succeed.
#
pkgname=rstudio-desktop-git
pkgver=20110311
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('custom')
depends=('r' 'java-runtime' 'phonon')
makedepends=('git' 'cmake' 'boost' 'apache-ant' 'unzip' 'openssl' 'bzip2' 'pam')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

build() {
  cd "$srcdir"
  echo "Entering $srcdir"
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server...."
    cd "$_gitname"
    git pull
  else
    msg "Cloning from GIT server...."
    git clone "$_gitroot" "$_gitname"
  fi

  cd "$srcdir/$_gitname"
  msg "GIT checkout done or server timeout"

  # Install gwt - this creates files inside the src package that are needed for the build
  msg "Downloading and installing gwt"
  cd "$srcdir/$_gitname/dependencies/common"
  ./install-gwt

  # Clone source into a new local repo for build
  rm -rf "$srcdir/$_gitname-build"
  #git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build" 

  # Configure with cmake in a new buld directory as recommended in the rstudio INSTALL file
  msg "Configuring the makefile with cmake"
  mkdir "$srcdir/$_gitname-build/build"
  cd "$srcdir/$_gitname-build/build"
  # Configure cmake to install to /opt/rstudio
  cmake -DRSTUDIO_TARGET=Desktop -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/opt/rstudio ..
}

package() {
  msg "Starting make and install...."
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install
  
  msg "Installing license...."
  install -D -m644 "$srcdir/$_gitname-build/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
 
